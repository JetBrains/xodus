buildscript {
    ext.kotlinVersion = '1.4.30'
    ext.kotlinLangVersion = '1.4'
    ext.kotlinCoroutinesVersion = '1.3.7'
    ext.dokkaVersion = '1.4.30'
    ext.kotlinLoggingVersion = '1.7.9'
    ext.annotationsVersion = '19.0.0'
    ext.licensePluginVersion = '0.15.0'
    ext.nexusPluginVersion = '0.21.2'
    ext.junitVersion = '4.13.2'
    ext.slf4jVersion = '1.7.30'
    ext.commonsCompressVersion = '1.21'
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'org.jetbrains.dokka' version "$dokkaVersion"
    id 'com.github.hierynomus.license' version "$licensePluginVersion"
    id 'io.codearte.nexus-staging' version "$nexusPluginVersion"
}

group = 'org.jetbrains.xodus'
version = hasProperty('xodusVersion') ? project.xodusVersion : ''
def isSnapshot = version.endsWith('SNAPSHOT')
def isDailyBuild = hasProperty('dailyBuild') ? project.dailyBuild : false
def mavenPublishUrl = hasProperty('mavenPublishUrl') ? project.mavenPublishUrl : ''
def mavenPublishUsername = hasProperty('mavenPublishUsername') ? project.mavenPublishUsername : ''
def mavenPublishPassword = hasProperty('mavenPublishPassword') ? project.mavenPublishPassword : ''
def signingKeyId = hasProperty('signingKeyId') ? project.signingKeyId : ''
def signingPassword = hasProperty('signingPassword') ? project.signingPassword : ''
def signingSecretKeyRingFile = hasProperty('signingSecretKeyRingFile') ? project.signingSecretKeyRingFile : '../key.gpg'

static def shouldDeploy(project) {
    return project.version.length() > 0 && !(project.name in ['benchmarks', 'samples'])
}

static def shouldApplyDokka(project) {
    return project.version.length() > 0 && !(project.name in ['benchmarks', 'samples', 'query'])
}


wrapper {
    gradleVersion = '6.5'
}

defaultTasks 'assemble'

// Use nexus-staging-plugin to workaround https://issues.sonatype.org/browse/OSSRH-5454
nexusStaging {
    username = mavenPublishUsername
    password = mavenPublishPassword
    delayBetweenRetriesInMillis = 30000
    stagingProfileId = "89ee7caa6631c4"
}

allprojects {
    repositories {
        maven { url "https://cache-redirector.jetbrains.com/repo1.maven.org/maven2" }
        // necessary for dokka 1.4.30
        maven { url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven" }
    }
}


subprojects {
    apply plugin: 'license'
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'org.jetbrains.dokka'

    sourceCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'
    group = rootProject.group
    version = rootProject.version
    archivesBaseName = rootProject.name + '-' + project.name

    license {
        header rootProject.file('license/copyright.ftl')
        strictCheck true
        ext.inceptionYear = 2010
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.owner = 'JetBrains s.r.o.'
        include "**/*.kt"
        include "**/*.java"
        mapping {
            kt = 'JAVADOC_STYLE'
        }
    }

    dependencies {
        implementation "org.apache.commons:commons-compress:$commonsCompressVersion"
        testImplementation group: 'junit', name: 'junit', version: "$junitVersion"
        testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: "$slf4jVersion"
    }

    // tests for most of sub-projects run with database encryption turned on
    if (!(project.name in ['benchmarks', 'compress', 'crypto', 'openAPI', 'samples', 'utils'])) {
        test {
            systemProperty 'exodus.cipherId', 'jetbrains.exodus.crypto.streamciphers.JBChaChaStreamCipherProvider'
            systemProperty 'exodus.cipherKey', '000102030405060708090a0b0c0d0e0f000102030405060708090a0b0c0d0e0f'
            systemProperty 'exodus.cipherBasicIV', '314159262718281828'
            // force v2 database format in tests
            systemProperty 'exodus.useVersion1Format', 'false'
            // force using bitmaps in entity-store implementation
            systemProperty 'exodus.entityStore.useIntForLocalId', 'true'
            // uncomment the following line to run tests in-memory
            //systemProperty 'exodus.log.readerWriterProvider', 'jetbrains.exodus.io.inMemory.MemoryDataReaderWriterProvider'
        }
        dependencies {
            testImplementation project(':crypto')
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    jar {
        manifest {
            attributes 'Implementation-Title': archivesBaseName, 'Implementation-Version': project.version
        }
    }

    test {
        minHeapSize = '1g'
        maxHeapSize = '1g'
        jvmArgs = ['-ea'
                   //, '-Dexodus.log.cache.useSoftReferences=true'
                   //, '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=2808'
        ]
        //testLogging.showStandardStreams = true
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        duplicatesStrategy 'exclude'
        includeEmptyDirs false
        from javadoc.outputDirectory
    }
    javadoc.failOnError = false
    // work around for Java 8 javadoc which is too strict
    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        duplicatesStrategy 'exclude'
        includeEmptyDirs false
        from project.sourceSets.main.java
        from project.sourceSets.main.kotlin
    }

    // configuring projects with Kotlin sources
    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        implementation "io.github.microutils:kotlin-logging:$kotlinLoggingVersion"
    }
    compileKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
            languageVersion = "$kotlinLangVersion"
            apiVersion = "$kotlinLangVersion"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
            languageVersion = "$kotlinLangVersion"
            apiVersion = "$kotlinLangVersion"
        }
    }
    if (shouldApplyDokka(project)) {
        dokkaJavadoc {
            dokkaSourceSets {
                configureEach {
                    reportUndocumented.set(false)
                }
            }
        }
        javadocJar {
            dependsOn dokkaJavadoc
            from dokkaJavadoc.outputDirectory
        }
    }

    artifacts {
        archives jar, javadocJar, sourceJar
    }

    if (!isSnapshot && signingKeyId.length() > 0) {
        ext.'signing.keyId' = signingKeyId
        ext.'signing.password' = signingPassword
        ext.'signing.secretKeyRingFile' = signingSecretKeyRingFile
    }

    afterEvaluate { project ->
        if (shouldDeploy(project) && !(isDailyBuild && project.name == 'tools')) {
            uploadArchives {
                repositories {
                    mavenDeployer {

                        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                        if (isDailyBuild) {
                            repository(url: "https://packages.jetbrains.team/maven/p/xodus/xodus-daily") {
                                authentication(userName: mavenPublishUsername, password: mavenPublishPassword)
                            }
                        } else {
                            repository(url: mavenPublishUrl) {
                                authentication(userName: mavenPublishUsername, password: mavenPublishPassword)
                            }
                        }

                        pom.project {
                            name 'Xodus'
                            description 'Xodus is pure Java transactional schema-less embedded database'
                            packaging 'jar'
                            url 'https://github.com/JetBrains/xodus'

                            scm {
                                url 'https://github.com/JetBrains/xodus'
                                connection 'scm:git:https://github.com/JetBrains/xodus.git'
                                developerConnection 'scm:git:https://github.com/JetBrains/xodus.git'
                            }

                            licenses {
                                license {
                                    name 'The Apache Software License, Version 2.0'
                                    url 'http://www.apache.org/license/LICENSE-2.0.txt'
                                    distribution 'repo'
                                }
                            }

                            developers {
                                developer {
                                    id 'JetBrains'
                                    name 'JetBrains Team'
                                    organization 'JetBrains s.r.o'
                                    organizationUrl 'http://www.jetbrains.com'
                                }
                            }
                        }
                    }
                }
            }
            signing {
                required { !isSnapshot && signingKeyId.length() > 0 && gradle.taskGraph.hasTask('uploadArchives') }
                sign configurations.archives
            }
        }
    }
}
